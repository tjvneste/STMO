{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots\n",
    "using LinearAlgebra\n",
    "using STMO"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Motivation\n",
    "\n",
    "Many more realistic optimization problems are characterized by constraints. For example, real-word systems often satisfy conservation laws, such as conservation of mass, of atoms or of charge. When designing objects, there are practical constraints of feasible dimensions, range of operations and limitations in materials. Another example is in probability, where a solution should satisfy the axioms of probability theory (probabilities are real values between 0 and 1 and the probabilities of all events should sum to 1).\n",
    "\n",
    "Other cases, we include constraints in our problem because they encode prior knowledge about the problem or to obtain solutions with certain desirable properties.\n",
    "\n",
    "In this chapter we discuss convex optimization problems with linear equality constraints (constraining the solution to a linear subspace) and convex inequality constrains (constraining the solution to convex subspace). Both types of constraints result again in a convex optimization problem.\n",
    "\n",
    "# Lagrange multipliers\n",
    "\n",
    "Lagrange multipliers are elegant ways of finding stationary points of a function of several variables given one or more constraints. We give a short introduction based on a geometric perspective.\n",
    "\n",
    "> IMPORTANT: most textbooks treat Lagrange multipliers from as maximization problems. Here they are treated as minimization problems to be consistent with other chapters.\n",
    "\n",
    "## Equality constraints\n",
    "\n",
    "Consider the following optimization problem:\n",
    "\n",
    "$$\n",
    "\\min_{\\mathbf{x}} f(\\mathbf{x})\n",
    "$$\n",
    "$$\n",
    "\\text{subject to } g(\\mathbf{x})=0\\,.\n",
    "$$\n",
    "\n",
    "![Convex optimization problem with an equality constraint. Here, the constraint is nonlinear.](Figures/Lagr1.png)\n",
    "\n",
    "For every point $\\mathbf{x}$ on the surface $g(\\mathbf{x})=0$, the gradient $\\nabla g(\\mathbf{x})$ is normal to this surface. This can be shown by considering a point $\\mathbf{x}+\\boldsymbol{\\epsilon}$, also on the surface. If we make a Taylor expansion around $\\mathbf{x}$, we have\n",
    "\n",
    "$$\n",
    "g(\\mathbf{x}+\\boldsymbol{\\epsilon})\\approx g(\\mathbf{x}) + \\boldsymbol{\\epsilon}^\\top\\nabla g(\\mathbf{x})\\,.\n",
    "$$\n",
    "\n",
    "Given that both $\\mathbf{x}$ and $\\mathbf{x}+\\boldsymbol{\\epsilon}$ lie on the surface it follows that $g(\\mathbf{x}+\\boldsymbol{\\epsilon})= g(\\mathbf{x})$. In the limit that $||\\boldsymbol{\\epsilon}||\\rightarrow 0$ we have that $\\boldsymbol{\\epsilon}^\\top\\nabla g(\\mathbf{x})=0$. Because $\\boldsymbol{\\epsilon}$ is parallel to the surface $g(\\mathbf{x})$, it follows that $\\nabla g(\\mathbf{x})$ is normal to the surface.\n",
    "\n",
    "![The same optimization problem, with some gradients of $f(\\mathbf{x})$ and $g(\\mathbf{x})$ shown.](Figures/Lagr2.png)\n",
    "\n",
    "We seek a point $\\mathbf{x}^\\star$ on the surface such that $f(\\mathbf{x})$ is minimized. For such a point, it should hold that the gradient w.r.t. $f$ should be parallel to $\\nabla g$. Otherwise, it would be possible to give a small 'nudge' to $\\mathbf{x}^\\star$ in the direction of $\\nabla f$ to decrease the function value, which would indicate that $\\mathbf{x}^\\star$ is not a minimizer. This figures below illustrate this point.\n",
    "\n",
    "![Point on the surface that is *not* a minimizer.](Figures/Lagr3.png)\n",
    "\n",
    "![Point on the surface that is a minimizer of $f$.](Figures/Lagr4.png)\n",
    "\n",
    "$$\n",
    "\\nabla f(\\mathbf{x}^\\star) + \\nu \\nabla g (\\mathbf{x}^\\star)=0\\,,\n",
    "$$\n",
    "with $\\nu\\neq 0$ called the *Lagrange multiplier*. The constrained minimization problem can also be represented by a *Lagrangian*:\n",
    "$$\n",
    "L(\\mathbf{x}, \\nu) \t\\equiv f(\\mathbf{x}) + \\nu g(\\mathbf{x})\\,.\n",
    "$$\n",
    "The constrained stationary condition is obtained by setting $\\nabla_\\mathbf{x} L(\\mathbf{x}, \\nu) =0$, the condition $\\partial  L(\\mathbf{x}, \\nu)/\\partial \\nu=0$ leads to the constraint equation $g(\\mathbf{x})=0$.\n",
    "\n",
    "## Inequality constraints\n",
    "\n",
    "The same argument can be made for inequality constraints, i.e. solving\n",
    "\n",
    "$$\n",
    "\\min_{\\mathbf{x}} f(\\mathbf{x})\n",
    "$$\n",
    "$$\n",
    "\\text{subject to } g(\\mathbf{x})\\leq0\\,.\n",
    "$$\n",
    "\n",
    "Here, two situations can arise:\n",
    "\n",
    "- **Inactive constraint**: the minimizer of $f$ lies in the region where $g(\\mathbf{x}) < 0$. This corresponds to a Lagrange multiplier $\\nu=0$. Note that the solution would be the same if the constraint was not present.\n",
    "- **Active constraint**: the minimizer of $f$ lies in the region where $g(\\mathbf{x}) > 0$. The solution of the constrained problem will lie on the bound where $g(\\mathbf{x})=0$, similar to the equality-constrained problem and corresponds to a Lagrange multiplier $\\nu>0$.\n",
    "\n",
    "Both scenarios are shown below:\n",
    "\n",
    "![Constrained minimization problem with an active inequality constraint. Optimum lies within the region where $g(\\mathbf{x})\\leq 0$. ](Figures/Lagr6.png)\n",
    "\n",
    "![Constrained minimization problem with an active inequality constraint. Optimum lies on the boundary of the region where $g(\\mathbf{x})\\leq 0$.](Figures/Lagr5.png)\n",
    "\n",
    "\n",
    "For both cases, the product $\\nu g(\\mathbf{x})=0$, the solution should thus satisfy the following conditions:\n",
    "$$\n",
    "g(\\mathbf{x}) \\leq 0\n",
    "$$\n",
    "$$\n",
    "\\nu \\geq 0\n",
    "$$\n",
    "$$\n",
    "\\nu g(\\mathbf{x})=0\\,.\n",
    "$$\n",
    "These are called the *Karush-Kuhn-Tucker* conditions.\n",
    "\n",
    "It is relatively straightforward to extend this framework towards multiple constraints (equality and inequality) by using several Lagrange multipliers.\n",
    "\n",
    "# Equality constrained convex optimization\n",
    "\n",
    "## Problem outline\n",
    "\n",
    "We will start with convex optimization problems with linear equality constraints:\n",
    "\n",
    "$$\n",
    "\\min_\\mathbf{x} f(\\mathbf{x})\n",
    "$$\n",
    "$$\n",
    "\\text{subject to } A\\mathbf{x}=\\mathbf{b}\n",
    "$$\n",
    "\n",
    "where $f : \\mathbb{R}^n \\rightarrow \\mathbb{R}$ is convex and twice continuously differentiable and $A\\in \\mathbb{R}^{p\\times n}$ with a rank $p < n$.\n",
    "\n",
    "The Lagrangian of this problem is\n",
    "\n",
    "$$\n",
    "L(\\mathbf{x}, \\boldsymbol{\\nu}) = f(\\mathbf{x}) + \\boldsymbol{\\nu}^\\top(A\\mathbf{x}-\\mathbf{b})\\,,\n",
    "$$\n",
    "with $\\boldsymbol{\\nu}\\in\\mathbb{R}^p$ the vector of Lagrange multipliers.\n",
    "\n",
    "A point $\\mathbf{x}^\\star\\in$ **dom** $f$ is optimal for the above optimization problem only if there is a $\\boldsymbol{\\nu}^\\star\\in\\mathbb{R}^p$ such that:\n",
    "\n",
    "$$\n",
    "A\\mathbf{x}^\\star = \\mathbf{b}, \\qquad \\nabla f(\\mathbf{x}^\\star) + A^\\top\\boldsymbol{\\nu}^\\star = 0\\,.\n",
    "$$\n",
    "\n",
    "We will reuse the same toy examples from the previous chapter, but add an equality constraint to both.\n",
    "\n",
    "- Simple quadratic problem:\n",
    "\n",
    "$$\n",
    " \\min_{\\mathbf{x}} \\frac{1}{2} (x_1^2 + 4 x_2^2)\n",
    "$$\n",
    "$$\n",
    " \\text{subject to }  x_1 - 2x_2 = 3\n",
    "$$\n",
    "\n",
    "- A non-quadratic function:\n",
    "\n",
    "$$\n",
    " \\min_{\\mathbf{x}}\\log(e^{x_1 +3x_2-0.1}+e^{x_1 -3x_2-0.1}+e^{-x_1 -0.1})\n",
    "$$\n",
    "$$\n",
    " \\text{subject to }  x_1 + 3x_2 = 0\n",
    "$$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "import STMO.TestFuns: fquadr, grad_fquadr, hess_fquadr\n",
    "import STMO.TestFuns: fnonquadr, grad_fnonquadr, hess_fnonquadr"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "![The two toy functions each with a linear constraint.](Figures/example_functions.png)\n",
    "\n",
    "## Equality constrained convex quadratic optimization\n",
    "\n",
    "Consider the following equality constrained convex optimization problem:\n",
    "\n",
    "$$\n",
    "\\min_\\mathbf{x}\\frac{1}{2}\\mathbf{x}^\\top P \\mathbf{x} + \\mathbf{q}^\\top \\mathbf{x} + r\n",
    "$$\n",
    "$$\n",
    "\\text{subject to }  A\\mathbf{x}=\\mathbf{b}\n",
    "$$\n",
    "\n",
    "where $P$ is symmetric.\n",
    "\n",
    "The optimality conditions are\n",
    "$$\n",
    "A\\mathbf{x}^\\star = \\mathbf{b}, \\quad P\\mathbf{x}^\\star+\\mathbf{q} +A^\\top\\boldsymbol{\\nu}^\\star=\\mathbf{0}\\,,\n",
    "$$\n",
    "which we can write as\n",
    "\n",
    "$$\n",
    "\\begin{bmatrix}\n",
    "P & A^\\top \\\\\n",
    "A & 0 \\\\\n",
    "     \\end{bmatrix}\n",
    "     \\begin{bmatrix}\n",
    "\\mathbf{x}^\\star\\\\\n",
    "\\boldsymbol{\\nu}^\\star\n",
    "     \\end{bmatrix}\n",
    "     =\n",
    "     \\begin{bmatrix}\n",
    "-\\mathbf{q} \\\\\n",
    "\\mathbf{b}\n",
    "     \\end{bmatrix}\\,.\n",
    "$$\n",
    "Note that this is a block matrix.\n",
    "\n",
    "> If $P$ is positive-definite, the linearly constrained quadratic minimization problem has an unique solution.\n",
    "\n",
    "Solving this linear system gives both the constrained minimizer $\\mathbf{x}^\\star$ as well as the Lagrange multipliers.\n",
    "\n",
    "**Assignment 1**\n",
    "\n",
    "1. Complete the code to solve linearly constrained quadratic systems.\n",
    "2. Use this code to solve the quadratic toy problem defined above."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    solve_constrained_quadratic_problem(P, q, A, b)\n",
    "\n",
    "Solve a linear constrained quadratic convex problem.\n",
    "\n",
    "Inputs:\n",
    "    - P, q: quadratic and linear parameters of\n",
    "            the linear function to be minimized\n",
    "    - A, b: system of the linear constraints\n",
    "\n",
    "Outputs:\n",
    "    - xstar: the exact minimizer\n",
    "    - nustar: the optimal Lagrange multipliers\n",
    "\"\"\"\n",
    "function solve_constrained_quadratic_problem(P, q, A, b)\n",
    "    p, n = size(A)  # size of the problem\n",
    "    # complete this code\n",
    "    solution = [P A'; A zeros(p, p)] \\ [-q; b]\n",
    "    xstar = solution[1:n]\n",
    "    nustar = solution[(n+1):end]\n",
    "    return xstar, nustar\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# solve the quadratic system with the linear constraint\n",
    "contour(-10:0.1:10, -10:0.1:10, (x1, x2)->fquadr((x1, x2)), xlabel=:x1, ylabel=:x2)\n",
    "plot!(x1->-(3-x1)/2, -10, 10, color=mygreen, label=:constraint)\n",
    "xstar, nustar = solve_constrained_quadratic_problem([1 0;0 4], [0,0], [1 -2], 3)\n",
    "x1star, x2star = xstar\n",
    "scatter!([x1star], [x2star], color=myorange, label=:minimizer)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Newton's method with equality constraints\n",
    "\n",
    "To derive $\\Delta \\mathbf{x}_{nt}$ for the following equality constrained problem\n",
    "\n",
    "$$\n",
    "\\min_\\mathbf{x}  f(\\mathbf{x})\n",
    "$$\n",
    "$$\n",
    "\\text{subject to }  A\\mathbf{x}=\\mathbf{b}\n",
    "$$\n",
    "\n",
    "we apply a second-order Taylor approximation at the point $\\mathbf{x}$, to obtain\n",
    "\n",
    "$$\n",
    "\\min_\\mathbf{v} \\hat{f}(\\mathbf{x} +\\mathbf{v}) = f(\\mathbf{x}) +\\nabla f(\\mathbf{x})^\\top \\mathbf{v}+ \\frac{1}{2}\\mathbf{v}^\\top \\nabla^2 f(\\mathbf{x}) \\mathbf{v}\n",
    "$$\n",
    "$$\n",
    "\\text{subject to } A(\\mathbf{x}+\\mathbf{v})=\\mathbf{b}\\,.\n",
    "$$\n",
    "\n",
    "Based on the solution of quadratic convex problems with linear constraints, the Newton $\\Delta \\mathbf{x}_{nt}$ step is characterized by\n",
    "\n",
    "$$\n",
    "\\begin{bmatrix}\n",
    " \\nabla^2 f(\\mathbf{x})&  A^\\top \\\\\n",
    "A & 0 \\\\\n",
    "     \\end{bmatrix}\n",
    "     \\begin{bmatrix}\n",
    "\\Delta \\mathbf{x}_{nt}\\\\\n",
    "\\mathbf{w}\n",
    "     \\end{bmatrix}\n",
    "     =\n",
    "     -\\begin{bmatrix}\n",
    "\\nabla f(\\mathbf{x}) \\\\\n",
    "A\\mathbf{x}-\\mathbf{b}\n",
    "     \\end{bmatrix}\n",
    "$$\n",
    "\n",
    "- If the starting point $\\mathbf{x}^{(0)}$ is chosen such that $A\\mathbf{x}^{(0)}=\\mathbf{b}$, the residual term vanishes and steps will remain in the feasible region. This is the **feasible start Newton method**.\n",
    "- If we choose an arbitrary $\\mathbf{x}^{(0)}\\in$ **dom** $f$, not satisfying the constraints, this is the **infeasible start Newton method**. It will usually converge rapidly to the feasible region (check the final solution!).\n",
    "\n",
    "Note that when we start at a feasible point, the residual vector $-(A\\mathbf{x}-\\mathbf{b})$ vanishes and the path will always remain in a feasible region. Otherwise we will converge to it.\n",
    "\n",
    "In this chapter, we will use a fixed step size. For Newton's method this usually leads to only a few extra iterations compared to an adaptive step size.\n",
    "\n",
    ">**input** starting point $\\mathbf{x}\\in$ **dom** $f$ (with $A\\mathbf{x}=\\mathbf{b}$ if using the feasible method), tolerance $\\epsilon>0$.\n",
    ">\n",
    ">**repeat**\n",
    ">\n",
    ">>    1. Compute the Newton step $\\Delta \\mathbf{x}_{nt}$ and decrement $\\lambda(\\mathbf{x})$.\n",
    ">>    2. *Stopping criterion*. **break** if $\\lambda^2/2\\leq \\epsilon$.\n",
    ">>    3. *Choose step size $t$*: either by line search or fixed $t$.\n",
    ">>    4. *Update*. $\\mathbf{x}:=\\mathbf{x}+t \\Delta \\mathbf{x}_{nt}$.\n",
    ">\n",
    ">**output** $\\mathbf{x}$\n",
    "\n",
    "Again, the convergence can be monitored using the Newton decrement:\n",
    "\n",
    "$$\n",
    "\\lambda^2(\\mathbf{x}) = \\lambda^2(\\mathbf{x}) = \\Delta \\mathbf{x}_{nt}^\\top \\nabla^2 f(\\mathbf{x})\\Delta \\mathbf{x}_{nt}\\,.\n",
    "$$\n",
    "\n",
    "The algorithm terminates when\n",
    "\n",
    "$$\n",
    "\\frac{\\lambda(\\mathbf{x})^2}{2} < \\epsilon\\,.\n",
    "$$\n",
    "\n",
    "The Newton decrement also indicates whether we are in or close to the feasible region.\n",
    "\n",
    "**Assignment 2**\n",
    "\n",
    "1. Complete the code for the linearly constrained Newton method.\n",
    "2. Use this code to find the minimum of the non-quadratic toy problem, defined above (compare a feasible and infeasible start)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "linear_constrained_newton(f, x₀, Df,\n",
    "              DDf, A, b; t::Real=0.25, ϵ::Real=1e-3,\n",
    "              tracker::Tracker=notrack)\n",
    "\n",
    "Newton's method for minimizing functions with linear constraints.\n",
    "\n",
    "Inputs:\n",
    "    - f: function to be minimized\n",
    "    - x₀: starting point (does not have to be feasible)\n",
    "    - Df: gradient of the function to be minimized\n",
    "    - DDf: hessian matrix of the function to be minimized\n",
    "    - A, b: linear constraints\n",
    "    - t: step size for each Newton step (fixed)\n",
    "    - ϵ: parameter to determine if the algorithm is converged\n",
    "    - verbose: print the number of steps?\n",
    "\n",
    "Outputs:\n",
    "    - xstar: the found minimum\n",
    "\"\"\"\n",
    "function linear_constrained_newton(f, x₀, Df,\n",
    "              DDf, A, b; t::Real=0.9, ϵ::Real=1e-4, verbose=false)\n",
    "    @assert 0.0 < t <= 1.0 \"stepsize t should be in (0, 1]\"\n",
    "    @assert A * x₀ ≈ b\n",
    "    x = x₀  # initial value\n",
    "    p, n = size(A)\n",
    "    nsteps = 0\n",
    "    while true\n",
    "        ddfx = DDf(x)\n",
    "        dfx = Df(x)\n",
    "        # calculate residual\n",
    "        Dx, _ = solve_constrained_quadratic_problem(ddfx, dfx, A, b - A * x) # complete!\n",
    "        λ² = Dx' * ddfx * Dx\n",
    "        if λ² < ϵ  # stopping criterion\n",
    "            break  # converged\n",
    "        end\n",
    "        # perform step\n",
    "        x .+= t * Dx\n",
    "        nsteps += 1\n",
    "    end\n",
    "    verbose && println(\"Converged in $nsteps steps\")\n",
    "    return x\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "contour(-5:0.1:5, -5:0.1:5, (x1, x2)->fnonquadr((x1, x2)), xlabel=:x1, ylabel=:x2)\n",
    "plot!(x1-> -x1/3, -5, 5, color=mygreen, label=:constraint)\n",
    "x₀ = [-0.0, 0.0]\n",
    "A = [1 3]\n",
    "b = [0]\n",
    "xstar = linear_constrained_newton(fnonquadr, x₀, grad_fnonquadr, hess_fnonquadr, [1 3], [0], verbose=true, t=1)\n",
    "x1star, x2star = xstar\n",
    "scatter!([x1star], [x2star], color=myorange, label=:minimizer)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# References\n",
    "\n",
    "- Boyd, S. and Vandenberghe, L., '*[Convex Optimization](https://web.stanford.edu/~boyd/cvxbook/bv_cvxbook.pdf)*'. Cambridge University Press (2004)\n",
    "- Bishop, C., *Pattern Recognition and Machine Learning*. Springer (2006)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.2",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
